\Module{muli\_aq}
%\begin{figure}
%  \centering{\includegraphics{uml-module-tree-3.mps}}
%  \caption{\label{fig:\ThisModule:Types}Klassendiagramm des Moduls \ThisModule}
%\end{figure}
aq ist eine Abkürzung für adaptive Quadratur. Mit aq\_class kann für eine beliebige Funktion $f:\mathbb{R}\rightarrow\mathbb{R}^n$ die Quasistammfunktion $F(x)=\int_x^1 f(y)\ \der\!y$ mittels adaptiver Quadratur ausgewertet und als Binärbaum von Segmenten $s_j=[x_{j-1},x_j]$ gespeichert werden. Zu jedem Segment werden $f(x_j),F(x_j)$ und $\exp[-F(x_j)]$ gespeichert.

$f$ wird mit der Trapezregel approximiert, entsprechend wird $F$ durch Parabeln approximiert. Dennoch ist die Approximation von $F$ nicht gleich Simpsons Regel, denn wir haben nur zwei Stützstellen $x_{j-1}$ und $x_j$ für jede Parabel.

Der Integrationsfehler $delta$ $\delta^\prime$ bzw. $\delta^\prime$ ergibt sich bei der Spaltung des Segments $s_j$ in zwei Untersegmente $s_j^\prime=[x_{j-1},y]$ und $s_j^{\prime\prime}=[y,x_j]$ durch die Differenz des alten und des neuen Integrals:
\begin{align}
  \delta^{\prime}&=\left|\frac{\big(f(y)-f_j(y)\big)\big(x_{j-1}-y\big)}{2}\right|\\
  \delta^{\prime\prime}&=\left|\frac{\big(f(y)-f_j(y)\big)\big(x_j-y\big)}{2}\right|
\end{align}
mit der alten Approximation $f_j$ des alten Segments $j$
\begin{equation}
  f_j(y)=f(x_j)-y\ \frac{f(x_j)-f(x_{j-1})}{x_j-x_{j-1}}.
\end{equation}
\section{Abhängigkeiten}
\use{muli\_basic}
\use{muli\_cuba}
\use{muli\_trapezium}
\use{muli\_fibonacci\_tree}
\section{Derived Types}
\TypeDef{aq\_class}
\begin{Verbatim}
  type,\Extends{identified\_type},abstract :: aq_class
     ! private
     \IC{Erweiterungen müssen durch is\_deferred\_initialised signalisieren, dass sie bereit sind.}
     logical :: \TC{is\_deferred\_initialised} = .false.
     \IC{Ist \CompRef{aq\_class}{err\_tree} bereit?}
     logical :: \TC{is\_error\_tree\_initialised} = .false.
     \IC{Wurden die internen Fehlerziele bestimmt?}
     logical :: \TC{is\_goal\_set} = .false.
     \IC{Ist alles bereit zur Integration?}
     logical :: \TC{is\_initialised} = .false.
     \IC{Wurde die Integration durchgeführt?}
     logical :: \TC{is\_run} = .false.
     \IC{Wurde das Fehlerziel erreicht?}
     logical :: \TC{is\_goal\_reached} = .false.
     \IC{Wurde \CompRef{aq\_class}{err\_tree} nach \CompRef{aq\_class}{int\_list} konvertiert?}
     logical :: \TC{is\_integrated} = .false.
     \IC{Die aktuelle Anzahl von Segmenten}
     integer(kind=dik) :: \TC{n\_nodes} = 0
     \IC{Die maximale Anzahl von Segmenten}
     integer(kind=dik) :: \TC{max\_nodes} = 10000
     \IC{Die Dimension von f}
     integer :: \TC{dim\_integral} = 1
     \IC{Das gegebene absolute Fehlerziel}
     real(kind=double) :: \TC{abs\_error\_goal} = 0D0
     \IC{Das gegebene relative Fehlerziel}
     real(kind=double) :: \TC{rel\_error\_goal} = 0.1D0
     \IC{Das berechnete absolute Fehlerziel, basierend auf der aktuellen}
     \IC{Schätzung des Integrals}
     real(kind=double) :: \TC{scaled\_error\_goal} = 0.0D0
     \IC{Schätzung des Integrals F(x_min)}
     real(kind=double) :: \TC{integral} = 1D0
     \IC{Aktueller absoluter Integrationsfehler}
     real(kind=double) :: \TC{integral\_error} = 0D0
     \IC{Integrationsintervall}
     real(kind=double),dimension(2) :: \TC{region} = (/0D0,1D0/)
     \IC{Zu Debuggingzwecken wird die Historie des Integrationsfehlers gespeichert.}
     \IC{Wenn die Historie oszilliert, dann ist der Fehler in f, also in der}
     \IC{Cuba-Integration zu groß.}
     real(kind=double),dimension(:,:),allocatable :: \TC{convergence}
     \IC{time stamps um die Performance des Allgorithmus zu überwachen.}
     real(kind=double) :: \TC{total\_time} = 0
     real(kind=double) :: \TC{loop\_time} = 0
     real(kind=double) :: \TC{int\_time} = 0
     real(kind=double) :: \TC{cuba\_time} = 0
     real(kind=double) :: \TC{init\_time} = 0
     real(kind=double) :: \TC{cpu\_time} = 0

     \IC{These variables *must* be initialised before the main loop may be called.}
     \IC{Additionaly the nodes and segments should be preprocessed by first_run}
     \IC{before the main loop may be called.}
     
     \IC{Das tatsächliche Fehlerziel.}
     real(kind=double) :: \TC{error\_goal} = 0D0
     \IC{Während der Integration werden die Segmente des Integranden nach ihrem}
     \IC{Integrationsfehler sortiert in diesem Binärbaum gespeichert.}
     class(\TypeRef{fibonacci\_root\_type}),pointer ::\TC{err\_tree}=>null()
     \IC{Nach erfolgreicher Integration werden die Segmente nach dem Skalenparameter}
     \IC{sortiert in dieser Liste gespeichert.}
     class(\TypeRef{muli\_trapezium\_list\_type}),pointer ::\TC{int\_list}=>null()
   contains
     \OverridesDeclaration{serializable\_class}
     procedure::\TbpDec{write\_to\_marker}{aq\_write\_to\_marker}
     procedure::\TbpDec{read\_from\_marker}{aq\_read\_from\_marker}
     procedure::\TbpDec{print\_to\_unit}{aq\_print\_to\_unit}
     procedure,nopass::\TbpDec{get\_type}{aq\_get\_type}
     procedure::\TbpDec{deserialize\_from\_marker}{aq\_deserialize\_from\_marker}
     \OriginalDeclaration
     procedure :: aq_initialize
     generic   ::\TbpDec{initialize}{aq\_initialize}
     procedure ::\TbpDec{print\_times}{aq\_print\_times}
     procedure ::\TbpDec{write\_convergence}{aq\_write\_convergence}
     ! init/ de-init
     procedure ::\TbpDec{reset}{aq\_reset}
     procedure ::\TbpDec{dealloc\_trees}{aq\_dealloc\_trees}
     procedure ::\TbpDec{finalize}{aq\_dealloc\_trees}
     procedure ::\TbpDec{init\_error\_tree}{aq\_init\_error\_tree}
     procedure ::\TbpDec{set\_rel\_goal}{aq\_set\_rel\_goal}
     procedure ::\TbpDec{set\_abs\_goal}{aq\_set\_abs\_goal}
     procedure ::\TbpDec{set\_goal}{aq\_set\_goal}
     procedure ::\TbpDec{check\_init}{aq\_check\_init}
     ! calculation
     procedure ::\TbpDec{main\_loop}{aq\_main\_loop}
     procedure ::\TbpDec{run}{aq\_run}
     procedure ::\TbpDec{integrate}{aq\_integrate}
     ! deferred
     procedure(evaluate_if),deferred :: evaluate
  end type aq_class
\end{Verbatim}
\section{Interfaces}
\begin{Verbatim}
  interface
     subroutine evaluate_if(this,x,y)
       use kinds!NODEP!
       import aq_class
       class(aq_class),intent(inout) :: this
       real(kind=double), intent(in) :: x
       real(kind=double), intent(out) ,dimension(:):: y
     end subroutine evaluate_if
   \end{Verbatim}
\Methods
\MethodsFor{aq\_class}
\OverridesSection{serializable\_class}

\TbpImp{aq\_write\_to\_marker}
\begin{Verbatim}
  subroutine aq_write_to_marker(this,marker,status)
    class(aq_class), intent(in) :: this
    class(marker_type),intent(inout)::marker
    integer(kind=dik),intent(out)::status  
    class(serializable_class),pointer::ser
    call marker%mark_begin("aq_class")
    call identified_write_to_marker(this,marker,status)
    call marker%mark("is_deferred_initialised",this&
         &%is_deferred_initialised)
    call marker%mark("is_error_tree_initialised",this&
         &%is_error_tree_initialised)
    call marker%mark("is_goal_set",this%is_goal_set)
    call marker%mark("is_initialised",this%is_initialised)
    call marker%mark("is_run",this%is_run)
    call marker%mark("is_goal_reached",this%is_goal_reached)
    call marker%mark("is_integrated",this%is_integrated)
    call marker%mark("n_nodes",this%n_nodes)
    call marker%mark("max_nodes",this%max_nodes)
    call marker%mark("dim_integral",this%dim_integral)    
    call marker%mark("abs_error_goal",this%abs_error_goal)
    call marker%mark("rel_error_goal",this%rel_error_goal)
    call marker%mark("scaled_error_goal",this%scaled_error_goal)
    call marker%mark("error_goal",this%error_goal)
    call marker%mark("integral",this%integral)
    call marker%mark("integral_error",this%integral_error)
    call marker%mark("region",this%region(1:2))
    ser=>this%err_tree
    call marker%mark_pointer("err_tree",ser)
    ser=>this%int_list
    call marker%mark_pointer("int_list",ser)
    call marker%mark_end("aq_class")
  end subroutine aq_write_to_marker
\end{Verbatim}

\TbpImp{aq\_read\_from\_marker}
\begin{Verbatim}
  subroutine aq_read_from_marker(this,marker,status)
    class(aq_class), intent(out) :: this
    class(marker_type),intent(inout)::marker
    integer(kind=dik),intent(out)::status  
    class(serializable_class),pointer::ser
    call marker%pick_begin("aq_class",status=status)
    call identified_read_from_marker(this,marker,status)
    call marker%pick("is_deferred_initialised",this%is_deferred_initialised&
         &,status)
    call marker%pick("is_error_tree_initialised",this&
         &%is_error_tree_initialised,status)
    call marker%pick("is_goal_set",this%is_goal_set,status)
    call marker%pick("is_initialised",this%is_initialised,status)
    call marker%pick("is_run",this%is_run,status)
    call marker%pick("is_goal_reached",this%is_goal_reached,status)
    call marker%pick("is_integrated",this%is_integrated,status)
    call marker%pick("n_nodes",this%n_nodes,status)
    call marker%pick("max_nodes",this%max_nodes,status)
    call marker%pick("dim_integral",this%dim_integral,status)    
    call marker%pick("abs_error_goal",this%abs_error_goal,status)
    call marker%pick("rel_error_goal",this%rel_error_goal,status)
    call marker%pick("scaled_error_goal",this%scaled_error_goal,status)
    call marker%pick("error_goal",this%error_goal,status)
    call marker%pick("integral",this%integral,status)
    call marker%pick("integral_error",this%integral_error,status)
    call marker%pick("region",this%region(1:2),status)
    call marker%pick_pointer("err_tree",ser)
    if(associated(ser))then
       select type(ser)
       class is (fibonacci_root_type)
          this%err_tree=>ser
       class default
          nullify(this%err_tree)
       end select
    end if
    call marker%pick_pointer("int_list",ser)
    if(associated(ser))then
       select type(ser)
       class is (muli_trapezium_list_type)
          this%int_list=>ser
       class default
          nullify(this%int_list)
       end select
    end if
    call marker%pick_end("aq_class",status)
  end subroutine aq_read_from_marker
\end{Verbatim}

\TbpImp{aq\_print\_to\_unit}
\begin{Verbatim}
  subroutine aq_print_to_unit(this,unit,parents,components,peers)
    class(aq_class),intent(in)::this
    integer,intent(in)::unit
    integer(kind=dik),intent(in)::parents,components,peers
    integer::ite
    class(serializable_class),pointer::ser
    if(parents>0)call identified_print_to_unit(this,unit,parents-1,components&
         &,peers)
    write(unit,'("Components of aq_class")')
    write(unit,'(a,L1)')"Deferred class initialised: ",this&
         &%is_deferred_initialised
    write(unit,'(a,L1)')"Error tree initialised:     ",this&
         &%is_error_tree_initialised
    write(unit,'(a,L1)')"Accuracy goal set:          ",this%is_goal_set
    write(unit,'(a,L1)')"Ready for run:              ",this%is_initialised
    write(unit,'(a,L1)')"Is run:                     ",this%is_run
    write(unit,'(a,L1)')"Accuracy goal reached:      ",this%is_goal_reached
    write(unit,'(a,L1)')"Integral calculated:        ",this%is_integrated
    write(unit,'(a,I10)')"Number of nodes:            ",this%n_nodes
    write(unit,'(a,I10)')"Maximal number of nodes:    ",this%max_nodes
    write(unit,'(a,I10)')"Dimension of integral:      ",this%dim_integral
    write(unit,'(a,E20.10)')"Given abs. error goal: ",this%abs_error_goal
    write(unit,'(a,E20.10)')"Given rel. error goal: ",this%rel_error_goal
    write(unit,'(a,E20.10)')"Guessed abs error goal:",this%scaled_error_goal
    write(unit,'(a,E20.10)')"Actual abs error goal: ",this%error_goal
    write(unit,'(a,E20.10)')"Integral               ",this%integral
    write(unit,'(a,E20.10)')"Estimated abs. error:  ",this%integral_error
    write(unit,'(a,E10.5,a,E10.5,a)')"Integration region =  (",this%region(1)&
         &," : ",this%region(2),")"
    ser=>this%err_tree
    call serialize_print_comp_pointer(ser,unit,parents,components,peers&
         &,"error tree")
    ser=>this%int_list
    call serialize_print_comp_pointer(ser,unit,parents,components,peers&
         &,"integral list")
  end subroutine aq_print_to_unit
\end{Verbatim}

\TbpImp{aq\_get\_type}
\begin{Verbatim}
  pure subroutine aq_get_type(type)
    character(:),allocatable,intent(out)::type
    allocate(type,source="aq_type")
  end subroutine aq_get_type
\end{Verbatim}
  
\TbpImp{aq\_deserialize\_from\_marker}
\begin{Verbatim}
  subroutine aq_deserialize_from_marker(this,name,marker)
    class(aq_class),intent(out)::this
    character(*),intent(in)::name
    class(marker_type),intent(inout)::marker
    class(serializable_class),pointer::ser
    allocate(muli_trapezium_type::ser)
    call marker%push_reference(ser)
    allocate(fibonacci_root_type::ser)
    call marker%push_reference(ser)
    allocate(fibonacci_leave_type::ser)
    call marker%push_reference(ser)
    allocate(fibonacci_node_type::ser)
    call marker%push_reference(ser)
    call serializable_deserialize_from_marker(this,name,marker)
    call marker%pop_reference(ser)
    deallocate(ser)
    call marker%pop_reference(ser)
    deallocate(ser)
    call marker%pop_reference(ser)
    deallocate(ser)
    call marker%pop_reference(ser)
    deallocate(ser)
  end subroutine aq_deserialize_from_marker
\end{Verbatim}
\OriginalSection{aq\_class}
\TbpImp{aq\_initialize}
\begin{Verbatim}
  subroutine aq_initialize(this,id,name,goal,max_nodes,dim,init)
    class(aq_class),intent(out) :: this
    integer(kind=dik),intent(in)::id,max_nodes
    integer,intent(in)::dim
    character,intent(in)::name
    real(kind=double)::goal
    real(kind=double),dimension(:),intent(in)::init
    call identified_initialize(this,id,name)
    this%rel_error_goal = goal!1d-4
    this%max_nodes=max_nodes
    call this%init_error_tree(dim,init)
  end subroutine aq_initialize
\end{Verbatim}
\TbpImp{aq\_print\_times}
\begin{Verbatim}
  subroutine aq_print_times(this)
    class(aq_class),intent(in) :: this
    print '(a,E20.10)',"Initialization time:  ",this%init_time
    print '(a,E20.10)',"Main loop time:       ",this%loop_time
    print '(a,E20.10)',"Integration time:     ",this%int_time
    print '(a,E20.10)',"Overall run time:     ",this%total_time
    print '(a,E20.10)',"Cuba integration time:",this%cuba_time
  end subroutine aq_print_times
\end{Verbatim}
\TbpImp{aq\_write\_convergence}
\begin{Verbatim}
  subroutine aq_write_convergence(this,unit)
    class(aq_class),intent(in) :: this
    integer,intent(in)::unit
    integer,dimension(2)::s
    integer::node
    if(allocated(this%convergence))then
       s=shape(this%convergence)
       do node=1,s(2)
          write(unit,fmt=*)node,this%convergence(1:2,node)
       end do
    end if
  end subroutine aq_write_convergence
\end{Verbatim}
! init/ de-init

\TbpImp{aq\_reset}
\begin{Verbatim}
  subroutine aq_reset(this)
    class(aq_class) :: this
    this%is_deferred_initialised = .false.
    this%is_error_tree_initialised = .false.
    this%is_goal_set = .false.
    this%is_initialised = .false.
    this%is_run = .false.
    this%is_goal_reached = .false.
    this%is_integrated = .false.
    this%n_nodes = 0
    this%max_nodes = 10000
    this%dim_integral=1
    this%abs_error_goal = 1D0
    this%rel_error_goal = 0.1D0
    this%scaled_error_goal = 0.0D0
    this%error_goal = 0.0D0
    this%integral = 0D0
    this%integral_error = 0D0
    this%region = (/0D0,1D0/)
    this%total_time = 0
    this%loop_time = 0
    this%int_time = 0
    this%init_time = 0
    call this%dealloc_trees()
  end subroutine aq_reset
\end{Verbatim}
\TbpImp{aq\_check\_init}
\begin{Verbatim}
  subroutine aq_check_init(this)
    class(aq_class) :: this
    this%is_initialised = this%is_error_tree_initialised .and. this%is_deferred_initialised
  end subroutine aq_check_init
\end{Verbatim}
\TbpImp{aq\_dealloc\_trees}
\begin{Verbatim}
  subroutine aq_dealloc_trees(this)
    class(aq_class) :: this
    if(associated(this%err_tree))then
       call this%err_tree%deallocate_all()
       deallocate(this%err_tree)
    end if
    if(associated(this%int_list))then
       call this%int_list%finalize()
       deallocate(this%int_list)
    end if
  end subroutine aq_dealloc_trees
\end{Verbatim}

\TbpImp{aq\_init\_error\_tree}
\begin{Verbatim}
  subroutine aq_init_error_tree(this,dim_integral,x_array)
    class(aq_class) :: this
    \IC{Wie viele Einträge hat der Rückgabewert von evaluate?}
    integer,intent(in)::dim_integral
    \IC{Eine geordnete Liste von Skalenparametern}
    real(kind=double), dimension(:), intent(in) :: x_array
    \IC{(x_j - x_\{j-1\})/2}
    real(kind=double) :: center
    \IC{Die Funktionswerte am linken Rand, in der Mitte und am rechten Rand des Intervalls.}
    real(kind=double), dimension(:),allocatable::l_val,c_val,r_val
    \IC{Jedes der gegebenen Intervalle wird in zwei Unterintervalle zerlegt, um eine}
    \IC{Abschätzung des Integrationsfehlees zu bekommen. In left\_node und right\_node}
    \IC{werden diese Intervalle gespeichert und in den Binärbaum eingefügt.}
    class(\TypeRef{muli\_trapezium\_type}),pointer :: left_node => null()
    class(\TypeRef{muli\_trapezium\_type}),pointer :: right_node => null()
    \IC{Die Anzahl der gegebenen x-Werte und die Nummer des aktuellen x-Werts.}
    integer :: x_size,pos
    \IC{Timer Start}
    call cpu_time(this%init_time)
    \IC{Signalisieren, dass die Bäume in einem undefinierten Zustand sind.}
    this%is_initialised=.false.
    this%integral=0D0
    this%dim_integral=dim_integral
    x_size = size(x_array)
    if (x_size<2) then
         write (*,'("aq_init_error_tree: I need at least two real values")')
    else
       \IC{In der Null-Komponente wird die Summe aller anderen Einträge gespeichert.}
       allocate(l_val(0:dim_integral-1))
       allocate(c_val(0:dim_integral-1))
       allocate(r_val(0:dim_integral-1))
       \IC{Der Integrationsbereich wird festgelegt.}
       this%region=(/x_array(1),x_array(x_size)/)
       if (x_size<3) then
          \IC{Wir haben nur ein Startsegment, das sich über den gesamten Integrationsbereich}
          \IC{erstreckt. Wir Teilen in der Mitte, denn der Binärbaum}
          \IC{\CompRef{aq\_class}{error\_tree} muss mindestens zwei Blätter haben.}
          center=(x_array(2)-x_array(1))/2D0
          \IC{Wir fordern die Funktionswerte an.}
          call this%evaluate(x_array(1),l_val)
          call this%evaluate(center,    c_val)
          call this%evaluate(x_array(2),r_val)
          \IC{Wir erzeugen ein neues Segment [x\_1,c].}
          allocate(left_node)
          call left_node%initialize(&
               &dim=dim_integral,&
               &r_position=center,&
               &d_position=center-x_array(1))
          call left_node%set_r_value(c_val)
          call left_node%set_d_value(c_val-l_val)
          \IC{Wir erzeugen ein neues Segment [c,x\_2].}
          allocate(right_node)
          call right_node%initialize(&
               &dim=dim_integral,&
               &r_position=x_array(2),&
               &d_position=x_array(2)-center)
          call right_node%set_r_value(r_val)
          call right_node%set_d_value(r_val-c_val)
       else
          \IC{wir haben genügend x-Werte, um einen minimalen Baum}
          \IC{\CompRef{aq\_class}{error\_tree} mit zwei Blättern zu initialisieren.}
          call this%evaluate(x_array(1),l_val)
          call this%evaluate(x_array(2),c_val)
          call this%evaluate(x_array(3),r_val)
          allocate(left_node)
          call left_node%initialize(&
               &dim=dim_integral,&
               &r_position=x_array(2),&
               &d_position=x_array(2)-x_array(1))
          call left_node%set_r_value(c_val)
          call left_node%set_d_value(c_val-l_val)
          allocate(right_node)
          call right_node%initialize(&
               &dim=dim_integral,&
               &r_position=x_array(3),&
               &d_position=x_array(3)-x_array(2))
          call right_node%set_r_value(r_val)
          call right_node%set_d_value(r_val-c_val)
       end if
       \IC{Die beiden Startblätter des Baums werden bereitgemacht}
       call left_node%update()
       call right_node%update()
       \IC{Der Wert für das Integral über diese Blätter wird abgeschätzt.}
       this%integral=sum(left_node%get_d_integral()+right_node%get_d_integral())
       if (.not. associated(this%err_tree)) then
          allocate(this%err_tree)
       end if
       \IC{Debugging}
       print *,left_node%measure()
       print *,right_node%measure()
       \IC{Der Baum wird mit den beiden Blättern initialisiert.}
       call this%err_tree%init_by_content(left_node,right_node)
       \IC{Wenn wir noch mehr Segmente haben, dann werden sie in den Baum aufgenommen.}
       if (x_size > 3) then
          do pos=4,x_size
             \IC{Fortschrittsanzeige. Die Intagrationen können einige Minuten dauern.}
             print *,"aq_init_error_tree",pos,"/",x_size
             \IC{Wir merken uns den Funktionswert am rechen Rand des letzten Segments.}
             \IC{Das ist der neue linke Funktionswert des neuen Segments.}
             l_val=right_node%get_r_value_array()
             \IC{Wir forden den Funktionswert am rechten Rand des neuen Intervalls an.}
             call this%evaluate(x_array(pos),r_val)
             \IC{Ein Missbrauch der Variablen, c\_val ist jetzt die Intervallänge.}
             c_val=r_val-l_val
             allocate(right_node)
             call right_node%initialize(&
                  &dim=dim_integral,&
                  &r_position=x_array(pos),&
                  &d_position=x_array(pos)-x_array(pos-1))
             call right_node%set_r_value(r_val)
             call right_node%set_d_value(c_val)
             call right_node%update()
             call this%err_tree%push_by_content(right_node)
             \IC{Das Gesamtintegral wird um das Integral über das neue Segment erhöht.}
             this%integral=this%integral+sum(right_node%get_d_integral())
          end do
          \IC{So viele Blätter hat der Baum jetzt.}
          this%n_nodes = x_size
       end if
       \IC{Der Baum ist wieder in einem definierten Zustand.}
       this%is_error_tree_initialised=.true.
    end if
    \IC{Da wir jetzt eine erste Abschätzung für das Integral haben, können wir}
    \IC{eine Abschätzung für das absolute Fehlerziel machen.}
    call this%set_goal()
    \IC{Damit ist alles Bereit für die adaptive Integration.}
    this%is_initialised=.true.
    \IC{Timer Stopp}
    call cpu_time(this%cpu_time)
    this%init_time=this%cpu_time-this%init_time
    this%cuba_time=this%init_time
    \IC{Debugging: Ab jetzt schreiben wir den aktuellen Integrationsfehler mit.}
    allocate(this%convergence(2,this%n_nodes:this%max_nodes))
  end subroutine aq_init_error_tree
\end{Verbatim}
\TbpImp{aq\_set\_abs\_goal}
\begin{Verbatim}
  subroutine aq_set_abs_goal(this,goal)
    class(aq_class) :: this
    real(kind=double) :: goal
    this%abs_error_goal = goal
    call this%set_goal
  end subroutine aq_set_abs_goal
\end{Verbatim}
\TbpImp{aq\_set\_rel\_goal}
\begin{Verbatim}
  subroutine aq_set_rel_goal(this,goal)
    class(aq_class) :: this
    real(kind=double) :: goal
    this%rel_error_goal = goal
    call this%set_goal
  end subroutine aq_set_rel_goal
\end{Verbatim}
\TbpImp{aq\_set\_goal}
Die angegebenen Fehlerziele werden auf Konsistenz geprüft. Aus dem relativen Fehler wird mithilfe der aktuellen Abschätzung des Integrals ein absoluter Fehler scaled\_error\_goal berechnet. Das Minimum aus abs\_error\_goal und scaled\_error\_goal wird das tatsächliche absulute Fehlerziel error\_goal.
\begin{Verbatim}
  subroutine aq_set_goal(this)
    class(aq_class) :: this
    this%scaled_error_goal = this%rel_error_goal*abs(this%integral)
    if ((this%scaled_error_goal==0D0).and.(this%abs_error_goal==0D0)) then
       this%is_goal_set = .false.
       this%error_goal = 0D0
    else
       if (this%scaled_error_goal == 0D0) then
          this%error_goal = this%abs_error_goal
       else
          if (this%abs_error_goal == 0D0) then
             this%error_goal = this%scaled_error_goal
          else
             this%error_goal = max(this%scaled_error_goal,this%abs_error_goal)
          end if
       end if
       if (this%error_goal > 0D0) then
          this%is_goal_set = .true.
       else
          this%is_goal_set = .false.
       end if
    end if
  end subroutine aq_set_goal
\end{Verbatim}
! calculation

\TbpImp{aq\_main\_loop}
Die eigentliche adaptive Quadratur findet in dieser Prozedur statt.
\begin{Verbatim}
  subroutine aq_main_loop(this)
    ! unsafe, when n_nodes < 4
    class(aq_class) :: this
    \IC{Das Blatt mit dem größten Integrationsfehler}
    class(\TypeRef{fibonacci\_leave\_type}), pointer :: rightmost
    
    class(\TypeRef{measurable\_class}), pointer :: content
    class(\TypeRef{muli\_trapezium\_type}),pointer :: new_node
    \IC{Wurde die maximale Anzahl von Blättern erreicht?}
    logical :: limit = .false.
    \IC{Die Stelle, bei der das Segment geteilt wird.}
    real(kind=double) :: center
    \IC{Der Funktionswert an dieser Stelle.}
    real(kind=double),dimension(:),allocatable::c_val
    allocate(c_val(0:this%dim_integral-1))
    loop:do
       \IC{Wir holen uns das Blatt mit den größten Integrationsfehler.}
       call this%err_tree%pop_right(rightmost)
       \IC{Wenn diese Bedingung erfüllt ist, dann ist auch der gesammte Fehler}
       \IC{kleiner als this\%error\_goal}
       if (rightmost < this%error_goal/this%n_nodes) then
          this%is_goal_reached = .true.
          exit loop
       else
          \IC{Wir holen uns das Integrationssegment aus dem Blatt.}
          call rightmost%get_content(content)
          \IC{Zugriff auf die speziellen Methoden von \TypeRef{muli\_trapezium\_type}}
          select type (content)
          class is (muli_trapezium_type)
             \IC{Fortschrittsanzeige}
             print&
             ('("nodes: ",I5," error: ",E14.7," goal: ",E14.7," node at: ",E14.7,"-",E14.7)'),&
                  this%n_nodes,&
                  rightmost%measure()*this%n_nodes,&
                  this%error_goal,&
                  content%get_l_position(),&
                  content%get_r_position()
             \IC{Debugging: Wir schreiben den Forschritt in den Abbruchbedingung mit.}
             this%convergence(1,this%n_nodes)=this%error_goal/this%n_nodes
             this%convergence(2,this%n_nodes)=rightmost%measure()
             \IC{Wir wollen das Segment in der Mitte teilen.}
             center = content%get_r_position()-content%get_d_position()/2D0
             call cpu_time(this%cpu_time)
             this%cuba_time=this%cuba_time-this%cpu_time
             \IC{Wir fordern den Funktionswert in der Mitte des Segments an.}
             call this%evaluate(center,c_val)
             call cpu_time(this%cpu_time)
             this%cuba_time=this%cuba_time+this%cpu_time
             \IC{Wir teilen das Segment in zwei neue Segmente.}
             \IC{Siehe \TbpRef{muli\_trapezium\_type}{split}}
             call content%split(c_val,center,new_node)
             \IC{content ist das rechte Segment und immer noch in rightmost enthalten.}
             \IC{Wir können also das Blatt rightmost wieder in den Baum einfügen.}
             call this%err_tree%push_by_leave(rightmost)
             \IC{Fur das linke Segment new\_node muss noch ein neues Blatt erzeugt werden.}
             call this%err_tree%push_by_content(new_node)
          end select
          this%n_nodes=this%n_nodes+1
          \IC{Wenn die maximale Zahl von Blättern erreicht ist, dann müssen wir erfolglos aufhören.}
          if (this%n_nodes > this%max_nodes) then
             limit = .true.             
             exit loop
          end if
       end if
    end do loop
    \IC{Ein Blatt halten wir noch in der Hand, wir legen es in den Baum zurück.}
    call this%err_tree%push_by_leave(rightmost)
  end subroutine aq_main_loop
\end{Verbatim}

\TbpImp{aq\_run}
Wrapper für \ProcRef{aq\_main\_loop}.
\begin{Verbatim}
  subroutine aq_run(this)
    class(aq_class) :: this
    call cpu_time(this%total_time)
    if (.not. this%is_error_tree_initialised) then
       call this%init_error_tree(this%dim_integral,this%region)
    end if
    this%is_run = .false.
    this%is_goal_reached = .false.
    call aq_main_loop(this)
    this%is_run = .true.
    call cpu_time(this%cpu_time)
    this%total_time=this%cpu_time-this%total_time
  end subroutine aq_run
\end{Verbatim}

\TbpImp{aq\_integrate}
Die eigentliche Integration ist schon fertig, aber die Integrationssegmente sind nach Integrationsfehler sortiert. Wir wollen jetzt einen Binärbaum erzeugen, in dem die Segmente nach den x-Werten sortiert sind.
\begin{Verbatim}
  subroutine aq_integrate(this,int_tree)
    class(aq_class) :: this
    class(\TypeRef{muli\_trapezium\_node\_class}),pointer :: node
    type(\TypeRef{muli\_trapezium\_tree\_type}),intent(out)::int_tree
    real(kind=double) :: sum
    this%is_integrated=.false.
    this%integral_error=0D0
    if (this%is_run) then
    call cpu_time(this%int_time)
       \IC{Umsortieren}
       call fibonacci_tree_resort_and_convert_to_trapezium_list&
         (this%err_tree,this%int_list)
       \IC{Die Integrale über die einzelnen Segmente aufaddieren}
       call muli_trapezium_list_integrate(this%int_list,this%integral,this%integral_error)
       \IC{Einen Baum aus der Liste machen}
       call this%int_list%to_tree(int_tree)
       this%is_integrated=.true.
       call cpu_time(this%cpu_time)
       this%int_time=this%cpu_time-this%int_time
    end if
  end subroutine aq_integrate
\end{Verbatim}
\MethodsNTB
\ProcImp{fibonacci\_tree\_resort\_and\_convert\_to\_trapezium\_list}
\begin{Verbatim}
  recursive subroutine fibonacci_tree_resort_and_convert_to_trapezium_list&
    (fib_tree,lin_list)
    \IC{usually, the tree is sorted by the sum of errors.}
    \IC{now it shall be sorted by the right position.}
    class(\TypeRef{fibonacci\_node\_type}),intent(in) :: fib_tree
    class(\TypeRef{fibonacci\_node\_type}),pointer :: leave
    class(\TypeRef{muli\_trapezium\_list\_type}),pointer,intent(out) :: lin_list
    class(\TypeRef{muli\_trapezium\_list\_type}),pointer :: left_list,right_list
    class(\TypeRef{muli\_trapezium\_node\_class}),pointer :: left_node,right_node,last_node
    class(\TypeRef{measurable\_class}),pointer :: content
    \IC{When at least one branch of the tree is itself a tree, i.e. each branch has}
    \IC{got at least two leaves, then process each branch and merge the results.}
    if (fib_tree%depth>1) then
       call fibonacci_tree_resort_and_convert_to_trapezium_list(fib_tree%left,left_list)
       call fibonacci_tree_resort_and_convert_to_trapezium_list(fib_tree%right,right_list)
       \IC{Now we got two sortet lists.}
       \IC{Which one's leftmost node has got the lowest value of "r_position"?}
       \IC{That one shall be the beginning of the merged list "lin_list".}
       if(left_list%is_left_of(right_list))then
          lin_list => left_list
          call left_list%get_right(left_node)
          right_node=>right_list
       else
          lin_list => right_list
          left_node=>left_list
          call right_list%get_right(right_node)
       end if
       last_node=>lin_list
       \IC{Everything is prepared for the algorithm: lin_list is the beginning of the}
       \IC{sorted list, last_node is it's end. left_node and right_node are the leftmost}
       \IC{nodes of the remainders of left_list and right_list. The latter will get}
       \IC{stripped from left to right, until one of them ends.}
       do while(associated(left_node).and.associated(right_node))          
          if (left_node%is_left_of(right_node)) then
             call last_node%append(left_node)
             call last_node%get_right(last_node)
             call left_node%get_right(left_node)
          else
             call last_node%append(right_node)
             call last_node%get_right(last_node)
             call right_node%get_right(right_node)
          end if
       end do
       \IC{Either left_list or right_list is completely merged into lin_list. The other}
       \IC{one gets appended to lin_list.}
       if (associated(left_node)) then
          call last_node%append(left_node)
       else
          call last_node%append(right_node)
       end if
       \IC{It's done.}
    else
       \IC{The tree has got two leaves at most. Is it more than one?}
       if (fib_tree%depth == 0) then
          \IC{Here fib_tree is a single leave with an allocated "content" componet of}
          \IC{type muli_trapezium_type. If "content" is not type compatible with}
          \IC{muli_trapezium_type, then this whole conversion cannot succeed. }
          \IC{We allocate a new node of type muli_trapezium_list_type. This list does}
          \IC{not contain the content of fib_tree, it *IS* a copy of the content, for}
          \IC{muli_trapezium_list_type is an extension of muli_trapezium_type.}
          select type (fib_tree)
          class is (fibonacci_leave_type)
             call fib_tree%get_content(content)
             select type (content)
             class is (muli_trapezium_type)
                call muli_trapezium_to_node(content,content%get_r_position(),list=lin_list)
             class default
                print *,"fibonacci_tree_resort_and_convert_to_trapezium_list: &
                     &Content of fibonacci_tree is not type compatible to &
                     &muli_trapezium_type"
             end select
          end select
       else
          \IC{Each branch of fib_tree is a single leave. We could call this soubroutine}
          \IC{for each branch, but we do copy and paste for each branch instead.}
          leave=>fib_tree%left
          select type (leave)
          class is (fibonacci_leave_type)
             call leave%get_content(content)
             select type (content)
             class is (muli_trapezium_type)
                call muli_trapezium_to_node(content,content%get_r_position(),list=left_list)
             class default
                print *,"fibonacci_tree_resort_and_convert_to_trapezium_list: &
                     &Content of fibonacci_tree is not type compatible to &
                     &muli_trapezium_type"
             end select
          end select
          leave=>fib_tree%right
          select type (leave)
          class is (fibonacci_leave_type)
             call leave%get_content(content)
             select type (content)
             class is (muli_trapezium_type)
                call muli_trapezium_to_node%
                  (content,content%get_r_position(),list=right_list)
             class default
                print *,"fibonacci_tree_resort_and_convert_to_trapezium_list: &
                     &Content of fibonacci_tree is not type compatible to &
                     &muli_trapezium_type"
             end select
          end select
          \IC{Finally we append one list to the other, the lowest value of "r_position"}
          \IC{comes first.}
          if (left_list%is_left_of(right_list)) then
             call left_list%append(right_list)
             lin_list=>left_list
          else
             call right_list%append(left_list)
             lin_list=>right_list
          end if
       end if
    end if
  end subroutine fibonacci_tree_resort_and_convert_to_trapezium_list
\end{Verbatim}

